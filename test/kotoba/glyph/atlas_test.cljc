(ns kotoba.glyph.atlas-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.glyph.atlas :as atlas]
            [kotoba.glyph.slot :as slot]))

(deftest ascii-procedural-atlas-matches-rust-test-ascii-atlas
  ;; Mirrors kami-text's `test_ascii_atlas` Rust test.
  (let [a (atlas/ascii-procedural-atlas 32.0)]
    (is (= 96 (count (:glyphs a))))
    (is (some? (get (:glyph-index a) "A")))
    (is (some? (get (:glyph-index a) "z")))
    (is (pos? (count (:sdf-data a))))))

(deftest ascii-procedural-atlas-grid-shape
  (let [a (atlas/ascii-procedural-atlas 32.0)]
    ;; 16 cols x 6 rows of cell (32*0.6=19, 32*1.0=32)
    (is (= (* 16 19) (:width a)))
    (is (= (* 6 32) (:height a)))))

(deftest ascii-procedural-atlas-glyph-key-index
  (let [a (atlas/ascii-procedural-atlas 18.0)
        g (get (:glyph-index a) "A")]
    (is (= g (get (:glyph-key-index a) (:key g))))
    (is (= (slot/glyph-key :latin (int \A)) (:key g)))))

(deftest pack-mono-atlas-uniform-grid
  (testing "16-column grid, uniform cell size"
    (let [keys (map #(slot/glyph-key :latin %) (range 40))
          packed (atlas/pack-mono-atlas keys 24.0)]
      (is (= 16 (:cols packed)))
      (is (= 3 (:rows packed))) ; ceil(40/16) = 3
      (is (= 40 (count (:cells packed))))
      (is (= (atlas/mono-cell-size 24.0) {:w (:cell-w packed) :h (:cell-h packed)}))
      ;; row-major placement
      (is (= 0 (:col (first (:cells packed)))))
      (is (= 0 (:row (first (:cells packed)))))
      (is (= 1 (:col (second (:cells packed)))))))
  (testing "empty ordered-keys falls back to a single fallback key"
    (let [packed (atlas/pack-mono-atlas [] 24.0)]
      (is (= 1 (count (:cells packed))))
      (is (= (slot/glyph-key :latin 0) (:key (first (:cells packed))))))))

(deftest pack-color-atlas-uniform-grid
  (testing "empty clusters -> nil (caller handles empty-atlas case)"
    (is (nil? (atlas/pack-color-atlas [] 18.0))))
  (testing "8-column grid"
    (let [packed (atlas/pack-color-atlas ["✨" "😀" "👍"] 18.0)]
      (is (= 8 (:cols packed)))
      (is (= 1 (:rows packed)))
      (is (= 3 (count (:cells packed))))
      (is (= "✨" (:cluster (first (:cells packed))))))))
